//******************************************************************************
// YAYA　ゴーストテンプレート
// コミュニケート辞書
//******************************************************************************


//******************************************************************************
//こちらから話しかける
//******************************************************************************

//この関数は現状メニューから呼ばれます
StartCommunicate
{
	//以下の関数は、隣に立っているゴーストを探して、
	//TalkTo_??? 関数を呼び出すものです。誰も居なければランダムトークをします。
	//yaya_tmpl_utilにあります。
	AYATEMPLATE.ExecuteTalkToTalk
}

//エミリに話しかける
TalkTo_Emily{
	"\0\s[0]エミリさん、出撃なのです。"
}


//その他の人に話しかける
TalkToOther{
	"\0\s[0]こんにちは、%(res_reference0)さん。"
}

TalkToNobody{
	"\0\s[0]‥‥‥‥"
}


//******************************************************************************
//話しかけられたので返事する
//******************************************************************************

//------------------------------------------------------------------------------
//OnCommunicateイベント
//------------------------------------------------------------------------------
OnCommunicate
{
	if reference[0] == "user" || reference[0] == "User"
	{
		//---- ユーザーからの話しかけ
		ReplyToUser
	}
	else
	{
		//---- ゴーストからの話しかけ
		//以下の関数は、ReplyTo_??? 関数を呼び出すものです。
		//yaya_tmpl_utilにあります。
		AYATEMPLATE.ExecuteReplyToTalk(reference[0])
	}
}

//------------------------------------------------------------------------------
//ユーザーからの話しかけ
//------------------------------------------------------------------------------
ReplyToUser
{
	if "名前" _in_ reference[1] && "覚えて" _in_ reference[1]
	{
		//---- ユーザー名入力開始
		"\0\s[0]%(username)？\e"
		teachusername = 1
	}
	else
	{
		//---- ユーザー名入力開始以外
		if teachusername == 1
		{
			//---- 直前が「名前覚えて」だったので名前を覚える
			username = reference[1]
			"\0\s[0]%(username)司令官？わかったのです。\e"
			teachusername = 0
		}
		else
		{
			//---- 名前覚え以外のさまざまな会話への返事
			TalkToUser
		}
	}
}

//------------------------------------------------------------------------------
//ユーザーへの返事（名前覚え以外）
//------------------------------------------------------------------------------
TalkToUser
{
	if "こんにちは" _in_ reference[1]
	{
		"\0\s[0]こんにちは。\e"
	}
	elseif "好き" _in_ reference[1] || "愛し" _in_ reference[1]
	{
		"\0\s[0]‥‥‥\w4私は男なのですが？\e"
	}
	elseif "ややめ" _in_ reference[1] && "好物" _in_ reference[1]
	{
		"\0\s[0]私の好きなものは、バグなのです。\e"
	}
	else
	{
		"\0\s[1]よく分からないのです‥‥\e"
	}
}

//------------------------------------------------------------------------------
//ゴーストからの話しかけ
//------------------------------------------------------------------------------

//エミリから話しかけられた
ReplyTo_Emily{
	res_reference0=reference[0]	//さらに相手に話しかける
	"\0\s[0]エミリさん？\e"
}


//その他の人から話しかけられた
ReplyToOther{
	if "こんにちは" _in_ reference[1] || "こんばんは" _in_ reference[1]
	{
		res_reference0=reference[0]	//さらに相手に話しかける
		"\0\s[0]こんにちは、%(res_reference0)さん。\e"
	}
	else
	{
		"\0\s[0]はい？\e"
	}
}

